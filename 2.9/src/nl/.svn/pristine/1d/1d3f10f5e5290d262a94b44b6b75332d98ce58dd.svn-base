package nl.bron 
{
	import flash.display.DisplayObject;
	import flash.display.DisplayObjectContainer;
	import flash.display.MovieClip;
	import flash.events.Event;
	import flash.geom.Point;
	import flash.geom.Rectangle;
	import flash.media.Video;
	import flash.net.NetStream;
	
	import nl.util.Net;
	
	public class brMovCell extends brCell
	{
		private var _lastFrame: int = -1;
		private var _playing: Boolean = false;
		
		private var _waitReady: int = 0;
		
		public function brMovCell()
		{
			super();
			addEventListener(Event.ENTER_FRAME, onEnter);
		}
		
		public static function loadMovResource(res: Resource): void
		{
			if (!res.param) {
				throw new Error("hostCell.param is null");
			}
			
			Net.getMov(res.param.url, function (v: Video, s: NetStream): void {
				res.drawMov(v, s);
			});
		}
		
		public static function updateState(hc: HostCell): void
		{
			doUpdateState(hc.actor, hc.param, hc.parent);
		}
		
		private static function doUpdateState(actor: DisplayObject, param: Object, parent: DisplayObjectContainer): void
		{
			if (!actor) return;
			
			actor.x = param.x;
			actor.y = param.y;
			actor.scaleX = actor.scaleY = param.zoom;
			actor.rotation = param.rotation;
			if (parent) {
				parent.filters = undefined;
				if (parent.parent) {
					parent.parent.filters = undefined;
				}
			}
		}
		
		private function onEnter(e: Event): void
		{
			if (!_hostCell.isResReady) {
				return;
			}
			var cur: int = _container.currentFrame;
			if (cur == _lastFrame || cur < _hostCell.param.start || cur >= _container.totalFrames) {
				stopMov();
			} else {
				if (!_playing || _lastFrame != (_container.currentFrame-1)) {
					playMov();
				}
			}
			_lastFrame = _container.currentFrame;
		}
		
		private function playMov(): void
		{
			stopMov();
			
			var p: Number = getPlayPos();
			if (p < 0) return;
			_hostCell.mov.seek(_container.currentFrame/25);
			_hostCell.mov.resume();
			_playing = true;
		}
		
		private function getPlayPos(): Number
		{
			var pp:Number = _container.currentFrame - _hostCell.param.start;
			/*if(pp > _hostCell.mov.length*25){
				if (!_loop) {
					return -1;		// 终止播放
				}
				pp = pp % _hostCell.mov.length*25;
			}*/
			return pp;
		}
		
		private function stopMov(): void
		{
			if (_playing) {
				_hostCell.mov.pause();
				_playing = false;
			}
		}
		
		override public function hitTestPoint(x: Number, y: Number, shapeFlag: Boolean = false): Boolean
		{
			return parent.getChildAt(parent.getChildIndex(this) - 1).hitTestPoint(x, y, shapeFlag);
		}
		
		public function rotatePoint(degrees : Number, pnt : Point = null) : void
		{
			if (pnt == null) { pnt = new Point(0, 0); }
			else { pnt = globalToLocal(pnt); }
			var p1 : Point = getRotatedPoint(new Point(param.x, param.y), pnt, degrees);
			param.x = param.x + pnt.x - p1.x;
			param.y = param.y + pnt.y - p1.y;
			param.rotation += degrees;
			doUpdateState(actor, param, this.parent);
		}
		
		public function zoomPoint(scale : Number, pnt : Point = null) : void
		{
			if (pnt == null) { pnt = new Point(0, 0); }
			else { pnt = globalToLocal(pnt); }
			param.x = param.x - (pnt.x - param.x) * (scale - 1);
			param.y = param.y - (pnt.y - param.y) * (scale - 1);
			param.zoom = param.zoom * scale;
			doUpdateState(actor, param, this.parent);
		}
		
		public function movePoint(pnt : Point) : void
		{
			var p : Point = globalToLocal(pnt);
			var po : Point = globalToLocal(new Point(0, 0));
			param.x = param.x + p.x - po.x;
			param.y = param.y + p.y - po.y;
			doUpdateState(actor, param, this.parent);
		}
		
		private function getRotatedPoint(po : Point, p : Point, degress : Number) : Point
		{
			p = new Point(p.x - po.x, p.y - po.y);
			var r : Number = degress / 180 * Math.PI;
			var x : Number = Math.cos(r) * p.x - Math.sin(r) * p.y;
			var y : Number = Math.cos(r) * p.y + Math.sin(r) * p.x;
			return new Point(x + po.x, y + po.y);
		}
		
		/**
		 * 设置图片刚好放在遮罩层下
		 */
		public function fitMask(): void
		{
			if (!actor) return;
			var scale: Number = 1;
			var mrk: DisplayObject = cellMask;
			trace(mrk);
			var rc: Rectangle = mrk.getBounds(parent);
			actor.scaleX = actor.scaleY = 1;
			actor.rotation = 0;
			
			if(param.maskHidden){
				if (rc.width / actor.width < rc.height / actor.height) {
					scale = rc.width / actor.width;
				} else {
					scale = rc.height / actor.height;
				}
			}else{
				if (rc.width / actor.width > rc.height / actor.height) {
					scale = rc.width / actor.width;
				} else {
					scale = rc.height / actor.height;
				}
			}
			
			param.zoom = scale;
			param.rotation = 0;
			
			param.x = mrk.x - (actor.width * scale - rc.width) / 2;
			param.y = mrk.y - (actor.height * scale - rc.height) / 2;
			
			doUpdateState(actor, param, this.parent);
		}
		
		/**
		 * 获取图片上方遮罩层
		 */
		public function get cellMask(): DisplayObject
		{
			return parent.getChildAt(parent.getChildIndex(this) - 1);
		}
		
		override public function copyEditParam(dest: Object, src: Object): void
		{
			dest.hidden = src.hidden;
			dest.x = src.x;
			dest.y = src.y;
			dest.zoom = src.zoom;
			dest.rotation = src.rotation;
			dest.url = src.url;
		}
	}
}
